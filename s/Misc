@ !UCDebug, ARM debugger for RISC OS.
@ https://github.com/fuentesp/UCDebug
@ Copyright (C) 2018  University of Cantabria
@
@ !UCDebug was developed by the Computer Architecture and Technology
@ Group at the University of Cantabria. A comprehensive list of authors
@ can be found in the file AUTHORS.txt.
@
@ You can reach the main developers at {fernando.vallejo, cristobal.camarero,
@ pablo.fuentes}@unican.es.
@
@ This program is free software: you can redistribute it and/or modify
@ it under the terms of the GNU General Public License as published by
@ the Free Software Foundation, either version 3 of the License, or
@ (at your option) any later version.
@
@ This program is distributed in the hope that it will be useful,
@ but WITHOUT ANY WARRANTY; without even the implied warranty of
@ MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
@ GNU General Public License for more details.
@
@ You should have received a copy of the GNU General Public License
@ along with this program.  If not, see <https://www.gnu.org/licenses/>.

.include  "s/Constants"

.global   EnableFPU
.global   DefCurCtx
.global   DefStackCtx
.global   ResetIRQtable
.extern   CurContext
.global   AuxCurContext
.extern   DebugAreaEnd
.global   loadAuxCurContext
.global   InitializeDbgKernel
.extern   ControllerBaseAddress
.global	  SWIReadChar
.extern	  Device

.data

.align 2
AuxCurContext:		.space 18*4	@ reserve space for 18 regs (R0-r15+CPSR+SPSR)
CtxModeAddrTable:	.word 0, CurContext+268, CurContext+236, CurContext+200, CurContext+52, CurContext+52, CurContext+52, CurContext+212, CurContext+52, CurContext+52, CurContext+52, CurContext+224

.text

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@ Function used to execute the OS_ReadC SWI
SWIReadChar:
ldr r1, =CurContext
swi OS_ReadC
str r0, [r1]
mov pc, lr


@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@ Function to activate the FPU (must be executed in privileged mode)
EnableFPU:
mrc p15, 0, r0, c1, c0, 2 @enable FPU
orr r0,r0,#0x300000 @ enable single precision
orr r0,r0,#0xC00000 @ also enable double precision
mcr p15, 0, r0, c1, c0, 2 @at this point, the FPU is enabled to work with single and double precision
mov r0,#0x40000000
VMSR FPEXC,r0
mov pc, lr


@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@
@ Set CurContext to Default values
@
DefCurCtx:
str lr,[sp, #-4]!
str r0,[sp, #-4]!
ldr r0,=CurContext
stm r0,{r0-r15}
ldr r1,[sp], #4
str r1,[r0] @ write program r0 in its place
mrs r1,CPSR
str r1,[r0, #OffCpsr]   @ CPSR of the user program
add r2, r0, #OffFPRegs
vstm r2, {s0-s31} @ write floating-point regs
vmrs r3, FPSCR
str r3, [r0, #OffFpscr] @ write FPSCR
ands r2, r1, #0xF	@ Mask to select usr mode
swieq OS_EnterOS        @ If in user mode, switch to SVC
bic r1, r1, #0xF        @ Regardless of previous mode,
orr r1, r1, #0x3	@ switch to SVC mode
msr CPSR, r1
str lr, [r0, #OffSvcRegs+4]	@ SVC lr
mrs r2, SPSR
str r2, [r0, #OffSvcRegs+8]	@ SVC spsr
bic r1, r1, #0x4	@ Switch from SVC to Abt mode
msr CPSR, r1
str lr, [r0, #OffAbtRegs+4]	@ Abt lr
mrs r2, SPSR
str r2, [r0, #OffAbtRegs+8]	@ Abt spsr
bic r1, r1, #0xF
orr r1, r1, #0xB	@ Switch from Abt to Und mode
msr CPSR, r1
str lr, [r0, #OffUndRegs+4]	@ Und lr
mrs r2, SPSR
str r2, [r0, #OffUndRegs+8]	@ Und spsr
bic r1, r1, #0x9
msr CPSR, r1		@ Switch from Und to IRQ mode
str lr, [r0, #OffIrqRegs+4]	@ IRQ lr
mrs r2, SPSR
str r2, [r0, #OffIrqRegs+8]	@ IRQ spsr
bic r1, r1, #0xF
orr r1, r1, #0x1	@ Switch to FIQ mode
msr CPSR, r1
add r2, r0, #OffFiqRegs
stm r2, {r8-r14}	@ FIQ regs
mrs r2, SPSR
str r2, [r0, #OffFiqRegs+7*4]	@ FIQ spsr
orr r1, r1, #0xF
msr CPSR, r1		@ Switch to SYS mode
str lr, [r0, #56]	@ Usr lr

ldr r1,[r0,#OffCpsr]
msr CPSR, r1		@ Switch back to original mode

bl DefStackCtx

ldr pc,[sp], #4


@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@
@ Set Stack to Default values for all modes; start by the end of the debugarea
@ and get the nearest lower address aligned to word. From there, dedicate
@ 512Bytes for each stack mode except User, that has 5KB.
@
DefStackCtx:

ldr r0,=CurContext
ldr r3, =DebugAreaEnd
ldr r3, [r3]            @ To properly set stack, get address of end of debugarea
bic r3, r3, #0x3        @ Ensure the address is aligned to word
str r3, [r0, #OffSvcRegs]	@ SVC sp
sub r3, r3, #0x200      @ (stack from SVC spans 512Bytes)
str r3, [r0, #OffAbtRegs]	@ Abt sp
sub r3, r3, #0x200      @ (stack from Abt spans 512Bytes)
str r3, [r0, #OffUndRegs]	@ Und sp
sub r3, r3, #0x200      @ (stack from Und spans 512Bytes)

str r3, [r0, #OffIrqRegs]	@ IRQ sp
sub r3, r3, #0x200      @ (stack from IRQ spans 512Bytes)
str r3, [r0, #OffFiqRegs+5*4]      @ FIQ sp
sub r3, r3, #0x200      @ (stack from FIQ spans 512Bytes)
str r3, [r0, #52]       @ Usr sp (will later be updated, when loading program)

mov pc, lr

@
@ Clear IRQ table (mainly for reloading codes)
@
ResetIRQtable:
ldr r0, =drivercount
mov r1, #0
str r1, [r0] @ set counter to zero

mov pc, lr

@
@ Update the context from the current user mode
@
loadAuxCurContext:
stmdb sp!, {r0-r12,lr}
ldr r0, =CurContext
ldm r0, {r0-r12}		@ Load values from user mode
ldr r0, =AuxCurContext
stm r0, {r0-r12}		@ Update them in current user mode
ldr r1, =CurContext
ldr r2, [r1]
str r2, [r0]			@ Update r0 in current user mode
add r2, r1, #52
ldm r2, {r3-r6}			@ Load sp, lr, pc & cpsr from user mode
add r2, r0, #52
stm r2, {r3-r6}			@ Update them in current user mode
ands r6, r6, #0xF		@ Get current user mode
beq lACC_end                    @ User mode
cmp r6, #0xF			@ System mode
beq lACC_end
ldr r3, =CtxModeAddrTable
ldr r3, [r3, r6, LSL #2]	@ Get @ of the mode to get regs from
ldm r3, {r3-r5}			@ Get regs
stm r2, {r3-r4}  		@ Update sp & lr in current user mode
str r5, [r0, #68]		@ Update spsr in current user mode
cmp r6, #0x1			@ If FIQ mode, more regs need to be loaded
bne lACC_end
add r2, r1, #OffFiqRegs
ldm r2, {r3-r7}			@ Get FIQ regs
add r2, r0, #32
stm r2, {r3-r7}			@ Update regs r0-r12 in current user mode
lACC_end:
ldmia sp!, {r0-r12, pc}

InitializeDbgKernel:
@ Determine underlying chipset, and load one base interrupt register
@ address or another, depending on which chipset is used.
@ It also maps the systimer and saves the address in a global variable.
swi OS_EnterOS
mrc P15, 0, r0, C0, C0, 0	@ Read CPU ID from Coprocessor 15 reg 0
swi OS_LeaveOS
ldr r2, =BCM2835_id
cmp r0, r2
ldreq r1, =Pi1_Irq_Addr		@ base address for ARM interrupt reg
ldr r2, =BCM2836_id
cmp r0, r2
ldreq r1, =Pi2_Irq_Addr		@ base address for ARM interrupt reg
ldr r2, =BCM2837_id
cmp r0, r2
ldreq r1, =Pi2_Irq_Addr		@ base address for ARM interrupt reg (same for Pi2 and Pi3)
mov r0, #13
mov r2, #0x228			@ 10 IRQ regs + 0x200 offset
swi OS_Memory
ldr r0, =ControllerBaseAddress
str r3, [r0]			@ store base address for IRQ regs

@ Maps the timer to memory
swi OS_EnterOS
mrc P15, 0, r0, C0, C0, 0	@ Read CPU ID from Coprocessor 15 reg 0
swi OS_LeaveOS
ldr r2, =BCM2835_id
cmp r0, r2
ldreq r1, =Pi1_ST_Addr		@ base address for System Timer controller
ldr r2, =BCM2836_id
cmp r0, r2
ldreq r1, =Pi2_ST_Addr		@ base address for System Timer controller
ldr r2, =BCM2837_id
cmp r0, r2
ldreq r1, =Pi2_ST_Addr		@ base address for SysTimer controller (same for Pi2 and Pi3)
mov r0, #13
mov r2, #0x100
swi 0x68 @ OS_Memory
ldr r0,=Device
str r3,[r0] @ Store the address of the timer
mov pc, lr


.end
